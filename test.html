<!DOCTYPE html>

<html>
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  
  <!--<link rel="stylesheet" type="text/css" href="" />-->
  <style>
    body {
      background: #020202;
    }
  </style>
  
  <!-- web fonts -->
  <!--<link rel="preconnect" href="https://fonts.gstatic.com">
  <link href="https://fonts.googleapis.com/css2?family=Roboto:ital,wght@0,400;0,700;1,400&display=swap" rel="stylesheet">-->
  
  <!-- OpenGL utilities -->
  <script type="text/javascript" src="twgl.min.js"></script>
  <!--<script type="text/javascript" src="gl-matrix-min.js"></script>-->
  
  <!-- shaders -->
  
  <script id="pass-vert" type="text/plain">
    #version 300 es
    
    // inputs
    in vec4 position;
    
    void main() {
      gl_Position = position;
    }
  </script>
  
  <script id="test-frag" type="text/plain">
    #version 300 es
    
    precision highp float;
    
    // outputs
    out vec4 fragColor;
    
    // resolution
    uniform vec2 resolution;
    uniform float shortdim;
    uniform float dpr;
    
    // triangulation
    uniform int next_edge[512];
    uniform float half_shear[128];
    uniform int n_edges;
    
    // left and right turns
    const mat4 left = mat4(
       1.,  0., 0., 0.,
       0.,  1., 0., 0.,
      -1.,  0., 1., 0.,
       0., -1., 0., 1.
    );
    const mat4 right = mat4(
      1., 0., -1.,  0.,
      0., 1.,  0., -1.,
      0., 0.,  1.,  0.,
      0., 0.,  0.,  1.
    );
    
    void main() {
      // the isometries that send the sides of the base triangle to the geoesic
      // from 0 to infinity in the upper half-plane
      mat4 stem[3];
      const float h = 0.5*sqrt(3.);
      stem[0] = mat4(
         1.,  0., -0.5,    h,
         0.,  1.,   -h, -0.5,
        -1.,  0.,  0.5,    h,
         0., -1.,   -h,  0.5
      );
      stem[1] = mat4(
        -0.5,   -h,  1.,  0.,
           h, -0.5,  0.,  1.,
         -1.,   0., 0.5,   h,
          0.,  -1.,   h, 0.5
      );
      stem[2] = mat4(
        -0.5,   -h,  1.,  0.,
           h, -0.5,  0.,  1.,
         0.5,   -h, -1.,  0.,
           h,  0.5,  0., -1.
      );
      
      // triangle walk
      vec2 z = (2.*gl_FragCoord.xy - resolution) / shortdim;
      if (dot(z, z) < 1.) {
        // departure
        vec4 u = vec4(z, 1., 0.);
        vec4 v;
        int edge = -1;
        vec3 color = vec3(1.);
        for (int k = 0; k < 3; k++) {
          v = stem[k] * u;
          if (dot(v.xy, v.zw) > 0.) {
            edge = k;
            break;
          }
        }
        if (edge < 0) {
          fragColor = vec4(1.);
          return;
        }
        
        // later steps
        for (int step = 0; step < 18; step++) {
          float x = half_shear[edge % n_edges];
          v *= vec4(vec2(1./x), vec2(x));
          if (dot(v.xy, v.zw) > dot(v.zw, v.zw)) {
            v = left * v;
            edge = next_edge[2*edge];
            color.r *= 0.8;
          } else if (dot(v.zw, v.xy) > dot(v.xy, v.xy)) {
            v = right * v;
            edge = next_edge[2*edge+1];
            color.g *= 0.8;
          } else {
            break;
          }
        }
        fragColor = vec4(color, 1.);
      } else {
        fragColor = vec4(0.);
      }
    }
  </script>
  
  <title>Hormonica</title>
</head>

<body>
  <canvas id="test" width="800" height="800"></canvas>
  
  <script>
    // --- get canvases and hook up interactions ---
    
    testCanvas = document.querySelector('#test');
    
    // --- initialize shaders ---
    
    const arrays = {
      position: [
        // northwest triangle
        -1, -1, 0,
        -1,  1, 0,
         1,  1, 0,
        // southeast triangle
        -1, -1, 0,
         1,  1, 0,
         1, -1, 0
      ]
    };
    
    var ctx = testCanvas.getContext('webgl2');
    var programInfo = twgl.createProgramInfo(ctx, ['pass-vert', 'test-frag']);
    var bufferInfo = twgl.createBufferInfoFromArrays(ctx, arrays);
    
    // --- define rendering routine ---
    
    function render(time) {
      // size canvas
      twgl.resizeCanvasToDisplaySize(testCanvas);
      
      // paint canvas
      ctx.useProgram(programInfo.program);
      twgl.setBuffersAndAttributes(ctx, programInfo, bufferInfo);
      twgl.setUniforms(programInfo, {
        resolution: [ctx.canvas.width, ctx.canvas.height],
        shortdim: Math.min(ctx.canvas.width, ctx.canvas.height),
        dpr: window.devicePixelRatio,
        half_shear: [7/6, 9/7, 6/9, 6/9, 7/6, 9/7],
        next_edge: [
          3, 5+6,
          4, 3+6,
          5, 4+6,
          1+6, 4,
          2+6, 5,
          0+6, 3,
          2, 1,
          0, 2,
          1, 0,
          5+6, 0+6,
          3+6, 1+6,
          4+6, 2+6
        ],
        n_edges: 6
      });
      twgl.drawBufferInfo(ctx, bufferInfo);
      
      // request next frame
      requestAnimationFrame(render);
    }
    
    // kick off animation
    requestAnimationFrame(render);
  </script>
</body>
</html>

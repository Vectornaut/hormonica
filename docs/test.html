<!DOCTYPE html>

<html>
<head>
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  
  <!--<link rel="stylesheet" type="text/css" href="" />-->
  <style>
    body {
      color: #fefefe;
      background: #040404;
    }
  </style>
  
  <!-- web fonts -->
  <!--<link rel="preconnect" href="https://fonts.gstatic.com">
  <link href="https://fonts.googleapis.com/css2?family=Roboto:ital,wght@0,400;0,700;1,400&display=swap" rel="stylesheet">-->
  
  <!-- OpenGL utilities -->
  <script type="text/javascript" src="twgl.min.js"></script>
  <!--<script type="text/javascript" src="gl-matrix-min.js"></script>-->
  
  <!-- shaders -->
  
  <script id="pass-vert" type="text/plain">
    #version 300 es
    
    // inputs
    in vec4 position;
    
    void main() {
      gl_Position = position;
    }
  </script>
  
  <script id="test-frag" type="text/plain">
    #version 300 es
    
    precision highp float;
    
    // outputs
    out vec4 fragColor;
    
    // resolution
    uniform vec2 resolution;
    uniform float shortdim;
    uniform float dpr;
    
    // triangulation
    uniform int next_edge[512];
    uniform float half_shear[128];
    uniform int n_edges;
    
    // --- pixel sampling ---
    
    const float A1 = 0.278393;
    const float A2 = 0.230389;
    const float A3 = 0.000972;
    const float A4 = 0.078108;
    
    // Abramowitz and Stegun, equation 7.1.27
    float erfc_appx(float t) {
      float r = abs(t);
      float p = 1. + A1*(r + A2*(r + A3*(r + A4*r)));
      float p_sq = p*p;
      float erfc_r = 1. / (p_sq*p_sq);
      return t < 0. ? (2. - erfc_r) : erfc_r;
    }
    
    // how much of a pixel's sampling distribution falls on a thickened line.
    // `width` is the line thickness, in pixels. `pattern_disp` is the pixel's
    // displacement from the line in pattern space
    float line_part(float width, float pattern_disp, float scaling, float r_px) {
      // find the displacement to the edge in the screen tangent space
      float screen_disp = pattern_disp / scaling;
      float screen_disp_px = screen_disp / r_px;
      
      // integrate our pixel's sampling distribution on the screen tangent space to
      // find out how much of the pixel falls within `width/2` of the line
      float lower = erfc_appx(screen_disp_px - 0.5*width);
      float upper = erfc_appx(screen_disp_px + 0.5*width);
      return 0.5*(lower - upper);
    }
    
    vec3 line_mix(vec3 stroke, vec3 bg, float width, float pattern_disp, float scaling, float r_px) {
      return mix(bg, stroke, line_part(width, pattern_disp, scaling, r_px));
    }
    
    vec4 line_mix(vec4 stroke, vec4 bg, float width, float pattern_disp, float scaling, float r_px) {
      return mix(bg, stroke, line_part(width, pattern_disp, scaling, r_px));
    }
    
    // --- triangle walk ---
    
    // left and right turns
    const mat4 left = mat4(
       1.,  0., 0., 0.,
       0.,  1., 0., 0.,
      -1.,  0., 1., 0.,
       0., -1., 0., 1.
    );
    const mat4 right = mat4(
      1., 0., -1.,  0.,
      0., 1.,  0., -1.,
      0., 0.,  1.,  0.,
      0., 0.,  0.,  1.
    );
    
    // display parameters
    const float view = 1.02;
    const vec3 bg_color = vec3(1./128.);
    
    void main() {
      // the isometries that send the sides of the base triangle to the geoesic
      // from 0 to infinity in the upper half-plane
      mat4 stem[3];
      const float h = 0.5*sqrt(3.);
      stem[0] = mat4(
         1.,  0., -0.5,    h,
         0.,  1.,   -h, -0.5,
        -1.,  0.,  0.5,    h,
         0., -1.,   -h,  0.5
      );
      stem[1] = mat4(
        -0.5,   -h,  1.,  0.,
           h, -0.5,  0.,  1.,
         -1.,   0., 0.5,   h,
          0.,  -1.,   h, 0.5
      );
      stem[2] = mat4(
        -0.5,   -h,  1.,  0.,
           h, -0.5,  0.,  1.,
         0.5,   -h, -1.,  0.,
           h,  0.5,  0., -1.
      );
      
      // triangle walk
      float r_px = view / shortdim; // the inner radius of a pixel in the Euclidean metric of the screen
      vec2 z = r_px * (2.*gl_FragCoord.xy - resolution);
      float r_sq = dot(z, z);
      if (r_sq < 1.) {
        // departure
        vec4 u = vec4(z, 1., 0.);
        vec4 v;
        int edge = -1;
        int first_edge; /* for test coloring */
        vec3 color = vec3(0.9, 0.9, 1.);
        for (int k = 0; k < 3; k++) {
          v = stem[k] * u;
          if (dot(v.xy, v.zw) > 0.) {
            edge = k;
            first_edge = k; /* for test coloring */
            break;
          }
        }
        if (edge < 0) {
          fragColor = vec4(1.);
          return;
        }
        
        // during the walk, we'll use these to see whether we're beyond the left
        // and right sides of the triangle. when we arrive, we'll use them
        // to find the signed distances to the sides of the triangle
        float m; // Re(ab')
        float q_a; // |a|^2
        float q_b; // |b|^2
        
        // later steps
        for (int step = 0; step < 18; step++) {
          // shear the opposite vertex to 1
          float x = half_shear[edge % n_edges];
          v *= vec4(vec2(1./x), vec2(x));
          
          // set up to find the signed distances to the left and right sides.
          // we look at `v` as a pair `(a, b)` of complex numbers
          m = dot(v.xy, v.zw); // Re(ab')
          q_a = dot(v.xy, v.xy); // |a|^2
          q_b = dot(v.zw, v.zw); // |b|^2
          
          if (m > q_b) {
            v = left * v;
            edge = next_edge[2*edge];
            color.r *= 0.8;
          } else if (m > q_a) {
            v = right * v;
            edge = next_edge[2*edge+1];
            color.g *= 0.8;
          } else {
            break;
          }
        }
        
        // --- arrival ---
        
        for (int k = 0; k < first_edge; k++) color = color.gbr;
        
        // the base side of the triangle is the geodesic from 0 to infinity
        // in the upper half-plane. the curve through `v` which is
        // equidistant from the base side is a ray which leaves 0 at some
        // angle `phi`. the distance `d_base` from `v` to the base side is
        // given by (Series, "Hyperbolic Geometry: MA 448", section 2.2.7)
        //
        //   t_base := tanh(d_base) = cos(phi).
        //
        float diff = sqrt(dot(v.xy - v.zw, v.xy - v.zw)); // |a - b|
        float t_base = m / sqrt(q_a * q_b);
        float t_left = (m - q_b) / (sqrt(q_b) * diff);
        float t_right = (m - q_a) / (sqrt(q_a) * diff);
        
        // find the nearest edge
        float t_near = t_base;
        int near_edge = edge;
        if (-t_left < abs(t_near)) {
          t_near = t_left;
          near_edge = next_edge[2*edge];
        }
        if (-t_right < abs(t_near)) {
          t_near = t_right;
          near_edge = next_edge[2*edge+1];
        }
        
        // find the conformal scale factor of the Poincare projection
        float proj_scaling = 2. / (1.-r_sq);
        
        // area-sample the nearest edge
        color = line_mix(bg_color, color, 2., t_near, proj_scaling, r_px);
        
        // area-sample the disk boundary
        color = line_mix(bg_color, color, 2., r_sq - 1., 2., r_px);
        fragColor = vec4(color, 1.);
      } else {
        fragColor = line_mix(vec4(bg_color, 1.), vec4(0.), 2., r_sq - 1., 2., r_px);
      }
    }
  </script>
  
  <title>Hormonica</title>
</head>

<body>
  <div>
    <button onclick="flip(3)">flip 3</button>
    <button onclick="flip(4)">flip 4</button>
    <button onclick="flip(5)">flip 5</button>
  </div>
  <canvas id="test" width="650" height="650"></canvas>
  
  <script>
    // --- get canvases and hook up interactions ---
    
    testCanvas = document.querySelector('#test');
    
    // --- initialize shaders ---
    
    const arrays = {
      position: [
        // northwest triangle
        -1, -1, 0,
        -1,  1, 0,
         1,  1, 0,
        // southeast triangle
        -1, -1, 0,
         1,  1, 0,
         1, -1, 0
      ]
    };
    
    var ctx = testCanvas.getContext('webgl2');
    var programInfo = twgl.createProgramInfo(ctx, ['pass-vert', 'test-frag']);
    var bufferInfo = twgl.createBufferInfoFromArrays(ctx, arrays);
    
    // triangulation and shear coordinates
    half_shear = [7/6, 9/7, 6/9, 6/9, 7/6, 9/7]
    next_edge = [
      3, 5+6,
      4, 3+6,
      5, 4+6,
      1+6, 4,
      2+6, 5,
      0+6, 3,
      2, 1,
      0, 2,
      1, 0,
      5+6, 0+6,
      3+6, 1+6,
      4+6, 2+6
    ]
    n_edges = half_shear.length
    
    function left(edge) {
      return next_edge[2*edge];
    }
    
    function right(edge) {
      return next_edge[2*edge+1];
    }
    
    function inv(edge) {
      return (edge + n_edges) % (2*n_edges);
    }
    
    function flip(edge) {
      // get relevant edges
      edge_inv = inv(edge);
      nigh = [right(edge), left(edge), right(edge_inv), left(edge_inv)];
      nigh_inv = nigh.map(inv);
      
      // find new shear coordinates
      half_shear_edge_old = half_shear[edge % n_edges];
      half_shear_edge_new = 1 / half_shear_edge_old;
      shear_edge_old = half_shear_edge_old * half_shear_edge_old;
      half_shear_even_mult = Math.sqrt(1 + shear_edge_old);
      half_shear_odd_mult = Math.sqrt(1 / (1 + 1 /shear_edge_old));
      
      // update shear coordinates
      half_shear[edge % n_edges] = half_shear_edge_new;
      half_shear[nigh[0] % n_edges] *= half_shear_even_mult;
      half_shear[nigh[1] % n_edges] *= half_shear_odd_mult;
      half_shear[nigh[2] % n_edges] *= half_shear_even_mult;
      half_shear[nigh[3] % n_edges] *= half_shear_odd_mult;
      
      // update paths out of quadrilateral
      next_edge[2*edge+1] = nigh[1];
      next_edge[2*edge] = nigh[2];
      next_edge[2*edge_inv+1] = nigh[3];
      next_edge[2*edge_inv] = nigh[0];
      
      // update paths into quadrilateral
      next_edge[2*nigh_inv[0]] = nigh[3];
      next_edge[2*nigh_inv[0]+1] = edge;
      next_edge[2*nigh_inv[1]] = edge_inv;
      next_edge[2*nigh_inv[1]+1] = nigh[2];
      next_edge[2*nigh_inv[2]] = nigh[1];
      next_edge[2*nigh_inv[2]+1] = edge_inv;
      next_edge[2*nigh_inv[3]] = edge;
      next_edge[2*nigh_inv[3]+1] = nigh[0];
    }
    
    // --- define rendering routine ---
    
    function render(time) {
      // size canvas
      twgl.resizeCanvasToDisplaySize(testCanvas);
      
      // paint canvas
      ctx.useProgram(programInfo.program);
      twgl.setBuffersAndAttributes(ctx, programInfo, bufferInfo);
      twgl.setUniforms(programInfo, {
        resolution: [ctx.canvas.width, ctx.canvas.height],
        shortdim: Math.min(ctx.canvas.width, ctx.canvas.height),
        dpr: window.devicePixelRatio,
        half_shear: half_shear,
        next_edge: next_edge,
        n_edges: 6
      });
      twgl.drawBufferInfo(ctx, bufferInfo);
      
      // request next frame
      requestAnimationFrame(render);
    }
    
    // kick off animation
    requestAnimationFrame(render);
  </script>
</body>
</html>
